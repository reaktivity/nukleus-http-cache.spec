#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newProxyAcceptRef ${nukleus:newRouteRef()} # external scope
property date ${http_cache:date()} # shared property
property date2 ${http_cache:datePlus(2)}

connect await ROUTED_PROXY
        "nukleus://http-cache/streams/source"
        option nukleus:route ${newProxyAcceptRef}
        option nukleus:window 8192
        option nukleus:transmission "half-duplex"
        option nukleus:group 5

write nukleus:begin.ext ${http:headers()
                              .item(":scheme", "http")
                              .item(":method", "GET")
                              .item(":path", "/path")
                              .item(":authority", "localhost:8080")
                              .build()}


connected

write close
read nukleus:begin.ext ${http:headers()
                             .item(":status", "200")
                             .item("content-length", "11")
                             .item("cache-control", "max-age=5")
                             .item("date", date)
                             .item("last-modified", date)
                             .build()}

read "hello world"

read closed
read notify REQUEST_CACHED


# INPUT STREAM 2
connect await REQUEST_CACHED
        "nukleus://http-cache/streams/source"
        option nukleus:route ${newProxyAcceptRef}
        option nukleus:window 8192
        option nukleus:transmission "half-duplex"
        option nukleus:group 5

write nukleus:begin.ext ${http:headers()
                              .item(":scheme", "http")
                              .item(":method", "GET")
                              .item(":path", "/path")
                              .item(":authority", "localhost:8080")
                              .item("cache-control", "min-fresh=6")
                              .build()}

connected

write close

read nukleus:begin.ext ${http:headers()
                             .item(":status", "200")
                             .item("content-length", "11")
                             .item("cache-control", "max-age=5")
                             .item("date", date2)
                             .item("last-modified", date2)
                             .build()}

read "hello alien"

read closed
